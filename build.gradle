plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
}

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.fabricmc.net/' }
    maven { url = 'https://maven.terraformersmc.com/releases/' }
    // Cloth Config is published on Shedaniel's Maven
    maven { url = 'https://maven.shedaniel.me/' }
    // Forge maven for distmarker annotations used by netmusic API
    maven { url = 'https://maven.minecraftforge.net/' }
    maven {
        url = 'https://cursemaven.com'
        content { includeGroup 'curse.maven' }
    }
}

configurations.all {
    resolutionStrategy {
        force 'io.netty:netty-buffer:4.1.118.Final'
        force 'io.netty:netty-codec:4.1.118.Final'
        force 'io.netty:netty-codec-http:4.1.118.Final'
        force 'io.netty:netty-common:4.1.118.Final'
        force 'io.netty:netty-handler:4.1.118.Final'
        force 'io.netty:netty-handler-proxy:4.1.118.Final'
        force 'io.netty:netty-resolver:4.1.118.Final'
        force 'io.netty:netty-transport:4.1.118.Final'
        force 'io.netty:netty-transport-classes-epoll:4.1.118.Final'
        force 'at.yawk.lz4:lz4-java:1.10.1'
        force 'org.lz4:lz4-java:1.10.1'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

    // Provides net.minecraftforge.api.distmarker.* annotations referenced by bundled API
    compileOnly "net.minecraftforge:forge:${minecraft_version}-52.0.26"

    modImplementation "curse.maven:net-music-978569:6659790"

    constraints {
        implementation("at.yawk.lz4:lz4-java:1.10.1")
        implementation("io.netty:netty-handler:4.1.118.Final")
    }
}

def resourceProps = [
        mod_id      : mod_id,
        mod_name    : mod_name,
        mod_version : mod_version,
        mod_license : mod_license,
        mod_authors : mod_authors,
        mod_description: mod_description,
        minecraft_version: minecraft_version,
        fabric_loader_version: fabric_loader_version
]

tasks.processResources {
    inputs.properties(resourceProps)
    filesMatching('fabric.mod.json') {
        expand resourceProps
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

jar {
    from('LICENSE') {
        rename { "${it}_${mod_id}" }
    }
}
