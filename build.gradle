plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
}

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.fabricmc.net/' }
    maven { url = 'https://maven.terraformersmc.com/releases/' }
    // Cloth Config is published on Shedaniel's Maven
    maven { url = 'https://maven.shedaniel.me/' }
    maven { url = 'https://api.modrinth.com/maven' }
    // YetAnotherConfigLib (YACL)
    maven { url = 'https://maven.isxander.dev/releases' }
    maven {
        url = 'https://cursemaven.com'
        content { includeGroup 'curse.maven' }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
    modImplementation "dev.isxander:yet-another-config-lib:${yacl_version}"

    // NetMusic (compile-only dependency - users install it separately at runtime)
    // Check both libs/ (committed to Git for CI) and run/mods/ (local dev convenience)
    def netMusicJar = file("libs/netmusic-1.2.1-fabric+mc1.21.1.jar")
    if (!netMusicJar.exists()) {
        netMusicJar = file("run/mods/netmusic-1.2.1-fabric+mc1.21.1.jar")
    }
    if (netMusicJar.exists()) {
        modCompileOnly files(netMusicJar)
    } else {
        throw new GradleException("NetMusic dependency not found. Please place netmusic-1.2.1-fabric+mc1.21.1.jar in libs/ or run/mods/ directory.")
    }
}

def resourceProps = [
        mod_id      : mod_id,
        mod_name    : mod_name,
        mod_version : mod_version,
        mod_license : mod_license,
        mod_authors : mod_authors,
        mod_description: mod_description,
        minecraft_version: minecraft_version,
        fabric_loader_version: fabric_loader_version
]

tasks.processResources {
    inputs.properties(resourceProps)
    filesMatching('fabric.mod.json') {
        expand resourceProps
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

jar {
    from('LICENSE') {
        rename { "${it}_${mod_id}" }
    }
}

loom {
    runs {
        client {
            // Only the client run gets the debugger; keeps Gradle daemon clean
            vmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005'
        }
    }
}
